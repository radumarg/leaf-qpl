module Compiler.TypeChecker.Data

import public Compiler.ScopeAndNameResolution.Data

import Frontend.ASTData
import Frontend.ASTPhases
import Frontend.Syntax.AST
import Frontend.Type

import Data.SortedMap

%default total

public export
TypedSymbolInfo : Type
TypedSymbolInfo = SymbolInfo (Maybe LeafType)

-- ReferenceRole describes syntax; AccessKind describes the typed operation on
-- a value. Neither records flow-dependent facts such as whether it was already
-- moved or whether this is its last use; those belong to ownership analysis.
public export
data AccessKind
  = ReadAccess          -- Observe/copy a value without transferring ownership.
  | WriteAccess         -- Initialize or replace a value.
  | MoveAccess          -- Transfer ownership elsewhere.
  | SharedBorrowAccess  -- Borrow without allowing mutation through this borrow.
  | MutableBorrowAccess -- Borrow with permission to mutate through this borrow.
  | ConsumeAccess       -- Consume a resource without transferring it onward.

public export
data PlaceRoot
  = LocalRoot SymbolId   -- A local variable or parameter, including a generated local.
  | TemporaryRoot NodeId -- Temporary storage produced by an expression, such as f() in f().x.

-- Projections are applied from left to right, starting at the place's root.
public export
data PlaceProjection
  = FieldProjection SymbolId -- The resolved field declaration, not its written spelling.
  | TupleProjection Nat      -- A zero-based tuple element index.
  | IndexProjection NodeId   -- The expression computing an array/slice index or range.
  | DereferenceProjection    -- The storage reached through a reference.

-- A field SymbolId alone identifies a declaration, not a particular value's
-- storage. For p.x, retain LocalRoot p and [FieldProjection x].
-- Different index-expression IDs or reference roots do not prove disjointness:
-- ownership analysis must account for dynamic indices and reference aliasing.
public export
record Place where
  constructor MkPlace
  root        : PlaceRoot
  projections : List PlaceProjection

public export
record AccessEvent where
  constructor MkAccessEvent
  kind       : AccessKind -- Classified using types and the operation's semantics.
  place      : Place      -- The affected local, temporary, field, element or referent.
  occurrence : AstInfo    -- The access's AST occurrence and diagnostic source span.

public export
record TypedModule where
  constructor MkTypedModule
  rootModule      : SymbolId
  rootScope       : ScopeId
  ast             : TypedSourceFile
  nodeScopes      : SortedMap NodeId ScopeId   -- lexical environment used to resolve a node
  symbols         : SortedMap SymbolId TypedSymbolInfo
  localVariables  : SortedMap SymbolId LocalVariableInfo
  references      : SortedMap SymbolId (SnocList SymbolReference)
  scopes          : SortedMap ScopeId ScopeInfo
  memberScopes    : SortedMap SymbolId ScopeId -- I have Point’s SymbolId. Which ScopeId contains its members?
  expressionTypes : SortedMap NodeId LeafType
  -- Events directly attributed to an expression/statement, in local evaluation
  -- order; accesses recorded on child nodes must not be duplicated here.
  -- Compile-time-only references (e.g. path qualifiers) produce no access event.
  -- SortedMap key order is NOT execution order: a later control-flow analysis
  -- determines legal moves/borrows, last uses and required cleanup.
  accesses        : SortedMap NodeId (List AccessEvent)
